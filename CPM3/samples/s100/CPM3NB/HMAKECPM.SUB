;r RMAC.COM
;r LINK.COM
;r GENCPM.COM
;
r GENCPM.DAT
;
r Z80.LIB
r CPM3.LIB
r MODEBAUD.LIB
;
r BIOSKRNL.ASM
r BDOS3.SPR
;
r SCB3.ASM
r CHARIO3.ASM
r MOVE3.ASM
;
r Hboot3.asm
r Hdrvtbl3.asm
r HFL3.ASM
r Hide3.asm
;
RMAC BIOSKRNL.ASM
;
RMAC SCB3.ASM
RMAC CHARIO3.ASM
RMAC MOVE3.ASM
;
RMAC HBOOT3.ASM
RMAC HDRVTBL3.ASM
RMAC HFL3.ASM
RMAC HIDE3.ASM
;
;A: is the IDE Drive
;
LINK BIOS3[B]=BIOSKRNL,SCB3,HBOOT3,CHARIO3,MOVE3,HDRVTBL3,HIDE3,HFL3
;
;Note GENCPM.DAT holds previous values
;This will be a Banked version. IDE Drive on A:
;B: & C: are 8" floppy disks, D: is a 5" floppy disk
;
gencpm auto
;
w cpm3.sys B
;
; Note this version is NON-BANKED and for a ZFDC FDC and IDE Drive 
;


